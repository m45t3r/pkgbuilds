# Maintainer: Thiago Kenji Okada <thiago DOT mast3r AT gmail DOT com>
# Based on ppsspp-git PKGBUILD from  Clement Guerin <geecko.dev@free.fr>
# and with Qt build instructions from here:
# https://github.com/hrydgard/ppsspp/issues/4449

pkgname=ppsspp-qt-git
pkgver=20131125.1
pkgrel=1
pkgdesc="A PSP emulator for Android, Windows, Mac, and Linux, written in C++ \
    with Qt UI."
arch=('i686' 'x86_64')
url="http://www.ppsspp.org/"
license=('GPL')
depends=('qt5-base' 'qt5-multimedia' 'sdl')
makedepends=('qt5-tools' 'git' 'gcc')
optdepends=()
provides=('ppsspp-qt')
conflicts=('ppsspp-qt4-git')
source=('ppsspp::git://github.com/hrydgard/ppsspp.git#branch=master'
        'PPSSPP.desktop')
md5sums=('SKIP'
         '351c298805113d5a223801948cda97a4')

pkgver() {
    cd "$srcdir/ppsspp"
    git describe --always | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd $srcdir/ppsspp
    git submodule update --init
}

build() {
    cd $srcdir/ppsspp

    mkdir build
    pushd build
    qmake ../Qt/PPSSPPQt.pro
    make
    popd
}

package() {
    cd $pkgdir

    install -Dm755 $srcdir/ppsspp/build/PPSSPPQt \
        $pkgdir/usr/bin/PPSSPPQt
    install -Dm644 $srcdir/ppsspp/assets/icon-114.png \
        $pkgdir/usr/share/icons/hicolor/114x114/apps/icon-114.png
    install -Dm644 $srcdir/PPSSPP.desktop \
        $pkgdir/usr/share/applications/PPSSPP.desktop
}

# vim: tabstop=8 expandtab shiftwidth=4 softtabstop=4
