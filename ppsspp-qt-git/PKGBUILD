# Maintainer: Thiago Kenji Okada <thiago DOT mast3r AT gmail DOT com>

pkgname=ppsspp-qt-git
pkgver=v0.9.8.r132.g1d21306
pkgrel=1
pkgdesc="A PSP emulator for Android, Windows, Mac, and Linux, written in C++ \
    with Qt UI."
arch=('i686' 'x86_64')
url="http://www.ppsspp.org/"
license=('GPL')
#SDL is not a hard dependency if you don't need joystick support
depends=('qt5-base' 'qt5-multimedia' 'sdl')
makedepends=('qt5-tools' 'git' 'gcc')
optdepends=()
provides=('ppsspp-qt')
#We only clone what we need to build instead of everything
source=('ppsspp::git://github.com/hrydgard/ppsspp.git#branch=master'
        'ppsspp-lang::git://github.com/hrydgard/ppsspp-lang'
        'ppsspp-ffmpeg::git://github.com/hrydgard/ppsspp-ffmpeg.git'
        'native::git://github.com/hrydgard/native'
        'PPSSPP.desktop')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         '18a59404e0e343b5b2b9ee25d33fd448') 

pkgver() {
    cd "$srcdir/ppsspp"
    git describe --always | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd $srcdir/ppsspp

    #Since PPSSPP use submodules, clone each submodule and change it's url
    #to our internal copy so we don't need to clone submodules on each build
    #WARNING: this method generates 3 copies of submodules instead of 1
    git config submodule.ffmpeg.url $srcdir/ppsspp-ffmpeg
    git submodule update ffmpeg

    git config submodule.lang.url $srcdir/ppsspp-lang
    git submodule update lang

    git config submodule.native.url $srcdir/native
    git submodule update native
}

build() {
    cd $srcdir/ppsspp

    mkdir build
    cd build
    qmake ../Qt/PPSSPPQt.pro
    make
}

package() {
    cd $pkgdir

    install -Dm755 $srcdir/ppsspp/build/PPSSPPQt \
        $pkgdir/usr/bin/PPSSPPQt
    install -Dm644 $srcdir/ppsspp/assets/icon-114.png \
        $pkgdir/usr/share/icons/hicolor/114x114/apps/icon-114.png
    #This is a modified version of official .desktop file from git
    install -Dm644 $srcdir/PPSSPP.desktop \
        $pkgdir/usr/share/applications/PPSSPP.desktop
}

# vim: tabstop=8 expandtab shiftwidth=4 softtabstop=4
