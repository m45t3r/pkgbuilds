# Maintainer: Thiago Kenji Okada <thiago DOT mast3r AT gmail DOT com>
# Contributor: uberushaximus <uberushaximus AT gmail DOT com>

pkgname=ppsspp-qt-git
pkgver=v1.0.r47.gf9974c7
pkgrel=1
# You can change this variable to any valid git object (e.g. stable versions)
_git_branch=master
pkgdesc="A PSP emulator for Android, Windows, Mac, and Linux, written in C++ \
    with Qt UI."
arch=('i686' 'x86_64')
url="http://www.ppsspp.org/"
license=('GPL')
# You can remove 'sdl' from 'depends' if you don't need joystick support
depends=('ffmpeg' 'qt5-base' 'qt5-multimedia' 'sdl2')
makedepends=('qt5-tools' 'git' 'gcc')
optdepends=()
provides=('ppsspp-qt')
install='ppsspp.install'
# We only clone what we need to build instead of everything
source=("ppsspp::git://github.com/hrydgard/ppsspp.git#branch=${_git_branch}"
        "ppsspp-lang::git://github.com/hrydgard/ppsspp-lang"
        "native::git://github.com/hrydgard/native"
        "armips::git://github.com/Kingcom/armips")
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP')

pkgver() {
    cd "${srcdir}/ppsspp"
    git describe --always | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/ppsspp"

    # Since PPSSPP use submodules, clone each submodule and change it's url
    # to our internal copy so we don't need to clone submodules on each build
    # WARNING: this method needs more disc space during the build
    git config submodule.lang.url ${srcdir}/ppsspp-lang
    git submodule update lang

    git config submodule.native.url ${srcdir}/native
    git submodule update native

    git config submodule.ext/armips.url ${srcdir}/armips
    git submodule update ext/armips
}

build() {
    cd "${srcdir}/ppsspp"

    # Build with Qt and system ffmpeg, disable packaging since this will be
    # done in package() function
    sh ./b.sh --qt --system-ffmpeg --no-package #--release
}

package() {
    install -Dm755 ${srcdir}/ppsspp/build-qt/ppsspp \
        ${pkgdir}/usr/bin/ppsspp
    install -Dm644 ${srcdir}/ppsspp/debian/ppsspp.desktop \
        ${pkgdir}/usr/share/applications/ppsspp.desktop
    install -Dm644 ${srcdir}/ppsspp/debian/ppsspp.1 \
        ${pkgdir}/usr/share/man/man1/ppsspp.1
    mkdir -p ${pkgdir}/usr/share/icons/hicolor
    cp -R ${srcdir}/ppsspp/assets/unix-icons/hicolor/* \
        ${pkgdir}/usr/share/icons/hicolor/
}

# vim: tabstop=8 expandtab shiftwidth=4 softtabstop=4
