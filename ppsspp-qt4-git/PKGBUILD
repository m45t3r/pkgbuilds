# Maintainer: Thiago Kenji Okada <thiago DOT mast3r AT gmail DOT com>
# Based on ppsspp-git PKGBUILD from  Clement Guerin <geecko.dev@free.fr>

pkgname=ppsspp-qt4-git
pkgver=20131127
pkgrel=1
pkgdesc="A PSP emulator for Android, Windows, Mac, and Linux, written in C++ \
    with Qt4 UI."
arch=('i686' 'x86_64')
url="http://www.ppsspp.org/"
license=('GPL')
depends=('qt4' 'sdl')
makedepends=('git' 'gcc')
optdepends=()
provides=('ppsspp-qt4')
conflicts=('ppsspp-qt-git')
source=('ppsspp::git://github.com/hrydgard/ppsspp.git#branch=master'
        'minidx9::git://github.com/hrydgard/minidx9'
        'ppsspp-lang::git://github.com/hrydgard/ppsspp-lang'
        'pspautotests::git://github.com/hrydgard/pspautotests'
        'ppsspp-redist::git://github.com/hrydgard/ppsspp-redist'
        'ppsspp-ffmpeg::git://github.com/hrydgard/ppsspp-ffmpeg.git'
        'native::git://github.com/hrydgard/native'
        'PPSSPP.desktop')
md5sums=('SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         'SKIP'
         '351c298805113d5a223801948cda97a4')

pkgver() {
    cd "$srcdir/ppsspp"
    git describe --always | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

prepare() {
    cd $srcdir/ppsspp

    #Since PPSSPP use submodules, clone each submodule and change it's url
    #to our internal copy so we don't need to clone submodules on each build
    git submodule init
    git config submodule.dx9sdk.url $srcdir/minidx9
    git config submodule.ffmpeg.url $srcdir/ppsspp-ffmpeg
    git config submodule.lang.url $srcdir/ppsspp-lang
    git config submodule.native.url $srcdir/native
    git config submodule.pspautotests.url $srcdir/pspautotests
    git config submodule.redist.url $srcdir/ppsspp-redist
    git submodule update
}

build() {
    cd $srcdir/ppsspp

    mkdir build
    pushd build
    qmake-qt4 ../Qt/PPSSPPQt.pro
    make
    popd
}

package() {
    cd $pkgdir

    install -Dm755 $srcdir/ppsspp/build/PPSSPPQt \
        $pkgdir/usr/bin/PPSSPPQt
    install -Dm644 $srcdir/ppsspp/assets/icon-114.png \
        $pkgdir/usr/share/icons/hicolor/114x114/apps/icon-114.png
    install -Dm644 $srcdir/PPSSPP.desktop \
        $pkgdir/usr/share/applications/PPSSPP.desktop
}

# vim: tabstop=8 expandtab shiftwidth=4 softtabstop=4
