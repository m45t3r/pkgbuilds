# Maintainer: Thiago Kenji Okada <thiago DOT mast3r AT gmail DOT com>
# Based on ppsspp-git PKGBUILD from  Clement Guerin <geecko.dev@free.fr>
# and with Qt build instructions from here:
# https://github.com/hrydgard/ppsspp/issues/4449

pkgname=ppsspp-qt4-git
pkgver=20131125
pkgrel=1
pkgdesc="A PSP emulator for Android, Windows, Mac, and Linux, written in C++ \
    with Qt4 UI."
arch=('i686' 'x86_64')
url="http://www.ppsspp.org/"
license=('GPL')
depends=('qt4' 'sdl')
makedepends=('git' 'gcc')
optdepends=()
provides=('ppsspp-qt')
source=('ppsspp::git://github.com/hrydgard/ppsspp.git#branch=master'
'ppsspp.desktop')
md5sums=('SKIP'
'7639cf40707503cfab0c51e566e92ad1')

pkgver() {
    cd "$srcdir/ppsspp"
    git describe --always | sed -E 's/([^-]*-g)/r\1/;s/-/./g'
}

build() {
    cd $srcdir/ppsspp

    git submodule init
    git submodule update

    export LDFLAGS="-lavutil -lavformat -lavcodec -lswscale -lswresample"

    mkdir build
    pushd build
    qmake-qt4 ../Qt/PPSSPPQt.pro
    make
    popd
}

package() {
    cd $pkgdir

    install -Dm755 $srcdir/ppsspp/build/PPSSPPQt \
        $pkgdir/usr/bin/PPSSPPQt
    install -Dm644 $srcdir/ppsspp/assets/icon.svg \
        $pkgdir/usr/share/pixmaps/ppsspp.svg
    install -Dm644 $srcdir/ppsspp.desktop \
        $pkgdir/usr/share/applications/ppsspp.desktop
}

# vim: tabstop=8 expandtab shiftwidth=4 softtabstop=4
